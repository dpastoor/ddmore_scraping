$PROB B.dat 4-cRUN249
$INPUT ID TIME AMT=DOSE DV EVID MDV CMT RATE DLVL ROUT=DROP FORM=DROP
PERD=DROP SBP=DROP DBP ATEN=DROP METP=DROP BISO S1A2 D1A2=DROP
H1A2=DROP S3A4=DROP D3A4=DROP H3A4=DROP UPH=DROP GGT=DROP CK=DROP LDH
SGPT SGOT=DROP BIL SCR=DROP HR=DROP CARD=DROP SMK AGE=DROP HGHT=DROP
WGHT=DROP GEND=DROP BMI BSA=DROP LBM=DROP FAT=DROP CRCL


$DATA ../data/Simulated_Lid_B04_ddmore.csv IGNORE='C'
IGNORE=(TIME.LT.0)

$SUBR ADVAN5 TRANS1
$MODEL
COMP=(CENTRAL DEFDOSE DEFOBSERVATION)
COMP=(MEGX)
COMP=(GX)
COMP=(26XYL)

$PK
IF(AMT.GT.0)TOD=TIME
TAD=TIME-TOD

P1=1
P2=1
P3=1
P4=1
P5=1
P6=1
P7=1
IF(DLVL.GT.2)P1=0
IF(BIL.GT.0.53)P2=0
IF(LDH.GT.195)P3=0
IF(CRCL.LE.52.7)P4=0
IF(S1A2.EQ.3)P5=0
IF(SGPT.GT.11)P6=0
IF(BMI.GT.27.93)P7=0

TK12=THETA(1)
K12=TK12
TK23=THETA(2)
K23=TK23
TK14=THETA(3)
K14=TK14

T1K30=P1*THETA(4)+(1-P1)*THETA(5)
T2K30=T1K30+(1-P2)*THETA(6)
IF(T2K30.LE.0)T2K30=0.0001

T3K30=T2K30+(1-P4)*THETA(7)
IF(T3K30.LE.0)T3K30=0.0001

T4K30=T3K30+(1-P5)*THETA(8)
IF(T4K30.LE.0)T4K30=0.0001

T5K30=T4K30+(1-P7)*THETA(9)
IF(T5K30.LE.0)T5K30=0.0001

TK30=T5K30+(1-P6)*THETA(10)
IF(TK30.LE.0)TK30=0.0001


K30=TK30*EXP(ETA(1))

T1K40=P3*THETA(11)+(1-P3)*THETA(12)
TK40=T1K40+(1-P6)*THETA(13)
IF(TK40.LE.0)TK40=0.0001

K40=TK40*EXP(ETA(2))

TV1=P1*THETA(14)+(1-P1)*THETA(15)
V1=TV1*EXP(ETA(3))
TVM=THETA(16)
V2=TVM
V3=TVM
V4=TVM


S1=V1
S2=V2
S3=V3
S4=V4

$ERROR

IPRED=F
IRES=DV-IPRED

Q1=1
Q2=0
Q3=0

Y1=F+EPS(1)
Y2=F+EPS(2)
Y3=F+EPS(3)
Y4=F+EPS(4)

IF(CMT.EQ.2) THEN
  Q1=0
  Q2=1
  Q3=0
ENDIF

IF(CMT.EQ.3) THEN
  Q1=0
  Q2=0
  Q3=1
ENDIF

IF(CMT.EQ.4) THEN
  Q1=0
  Q2=0
  Q3=0
ENDIF

Y=Y1*Q1+Y2*Q2+Y3*Q3+Y4*(1-Q1)*(1-Q2)*(1-Q3)

W     = SQRT(SIGMA(1,1))       
IWRES = IRES/W			; IWRES for LID

$THETA
(0.03 FIX)     ;K12
(0,1)          ;K23
(0.007 FIX)    ;K14
(0,1)          ;K30  FOR DLVL <= 2
(0,1)          ;K30  FOR DLVL >  2

(-2,0.1,2)     ;MODIFICATION OF K30 FOR BIL > 0.53
(-2,0.1,2)     ;MODIFICATION OF K30 FOR CRCL <= 52.7
(-2,0.1,2)     ;MODIFICATION OF K30 FOR S1A2 = 3 (PRESENT)
(-2,0.1,2)     ;MODIFICATION OF K30 FOR BMI > 27.93
(-2,0.1,2)     ;MODIFICATION OF K30 FOR SGPT > 11

(0,1)          ;K40  FOR LDH <= 195
(0,1)          ;K40  FOR LDH >  195
(-2,0.1,2)     ;MODIFICATION OF K40 FOR SGPT > 11

(1,1000)       ;V1   FOR DLVL <= 2
(1,1000)       ;V1   FOR DLVL >  2
(100 FIX)      ;V2,3,4

$OMEGA
0.1            ;ETA ON K30
0.1            ;ETA ON K40
0.1            ;ETA ON V1

$SIGMA

10            ;EPS ADD. FOR LID 
10            ;EPS ADD. FOR MEGX
10            ;EPS ADD. FOR GX
10            ;EPS ADD. FOR 26-XYL  

$EST MAXEVAL=3000 PRINT=5 SIGDIG=3 NOABORT POSTHOC
$COV 

$TABLE MDV EVID ID TIME TAD DV CMT AMT RATE DLVL BIL CRCL S1A2 BMI SGPT LDH IPRED 
	IRES CWRES IWRES NPDE ETA1 ETA2 ETA3 NOPRINT FILE=ddmore_final_run249.tab ONEHEADER


